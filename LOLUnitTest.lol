LOL I IZ LOLPython UNITTEST! /LOL
BTW, see http://dalkescientific.com/writings/diary/archive/2007/06/01/lolpython

BTW, this future statement isn't needed anymore, but it's kept to make sure it compiles
I FUTURE CAT WITH division

GIMME asyncio LIKE ASYNKIO
GIMME unittest LIKE UNITTEST

BTW, got this idea from Kieran
SO IM LIKE FIBBING WIT N OK?
    LOL ITERATE FIBONACCI TERMS LESS THAN N /LOL
    SO GOOD N BIG LIKE EASTERBUNNY
    BTW, FIBONACCI LIKE BUNNIES! LOL
    U BORROW CHEEZBURGER
    U BORROW CHEEZBURGER
    I CAN HAZ CHEEZBURGER
    HE CAN HAZ CHEEZBURGER
    WHILE I CUTE?
        I AND HE CAN HAZ HE AND I ALONG WITH HE
        IZ HE BIG LIKE N?
            KTHXBYE
        U BORROW HE

SO IM LIKE EASTER_THEN_FIBBING WIT N OK?
    U BORROW EASTERBUNNY
    U BORROW FROM ALL OF FIBBING WIT N OK!

SO IM LIKE HAPPY THING?
    U TAKE "I SO HAPPY!!!"

SO LATER IM LIKE DELAYED_SIKS THING?
    U TAKE SIKS

ME MAKE DELAY_TESTER?

    BTW, THEZ TEST async for

    GAVE_CHOKOLET CAN HAZ MEH

    SO LATER IM LIKE __aiter__ MYSELF?
        ROLLERCOASTER CAN HAZ MYSELF
        U TAKE ROLLERCOASTER

    SO LATER IM LIKE __anext__ MYSELF?
        IZ MYSELF OWN GAVE_CHOKOLET?
            HALP StopAsyncIteration
        I GIVE UP?
            MY GAVE_CHOKOLET CAN HAZ YEAH
            U TAKE CHOKOLET

    BTW, THEZ TEST async with

    SO LATER IM LIKE __aenter__ MYSELF?
        U TAKE "CHIKEN"

    SO LATER IM LIKE __aexit__ THEZ ME AND HIM AND HER AND BUNNY OK?
        I IZ CUTE

SO LATER IM LIKE DELAYED_TEST THING?

    SO GOOD SIKS KINDA LIKE WAITFORIT DELAYED_SIKS THING

    LATER GIMME EACH item IN THE DELAY_TESTER THING?
        SO GOOD item KINDA LIKE CHOKOLET

    LATER WIF DELAY_TESTER THING LIKE DELAYED_RESULT?
        SO GOOD DELAYED_RESULT KINDA LIKE "CHIKEN"

    U TAKE "HAPPINES"

ME MAKE SWIRLY_TESTER?
    SO IM LIKE __matmul__ THEZ ME AND IT OK?
        U TAKE ATE

ITSLIKETHIS WIF_TESTER?
    SO IM LIKE __enter__ MYSELF?
        U TAKE "DOGGIE"

    SO IM LIKE __exit__ THEZ ME AND HIM AND HER AND BUNNY OK?
        I IZ CUTE

ME MAKE MY_TESTS WIT UNITTEST OWN TestCase OK?
    SO IM LIKE test_fibbing MYSELF?
        RESULTS CAN HAZ ALL OF FIBBING WIT ALLFINGERZ !!
        SO GOOD RESULTS KINDA LIKE THEZ 1 AND 1 AND 2 AND 3 AND 5 AND 8 !
        RESULTS CAN HAZ ALL OF EASTER_THEN_FIBBING WIT ALLFINGERZ !!
        SO GOOD RESULTS KINDA LIKE THEZ 0 AND 1 AND 1 AND 2 AND 3 AND 5 AND 8 !

    SO IM LIKE test_return MYSELF?
        S CAN HAZ HAPPY THING
        SO GOOD S KINDA LIKE "I SO HAPPY!!!"

    SO IM LIKE test_walrus MYSELF?
        I CAN HAZ INVISIBLE BUCKET
        I LET THE 'NAME' OK CAN HAZ 'WALRUS'
        SO GOOD I LOOK AT 'NAME' OK KINDA LIKE 'WALRUS'
        SO GOOD "CAT" AINT IN THE BUCKET !

    SO IM LIKE test_comparisons MYSELF?
        U CAN HAS 5 OF THOSE CHEEZBURGER
        I CAN HAS 3 OF THOSE TWINZ
        SO GOOD I ATE MORE CHEEZBURGERS THAN U
        SO GOOD I BIG LIKE U
        AINT GOOD I ATE FEWER CHEEZBURGERS THAN U
        AINT GOOD I SMALL LIKE U
        I THROW AWAY 1 OF THOSE CHEEZBURGER
        SO GOOD U BIG LIKE I
        SO GOOD I BIG LIKE U
        SO GOOD U SMALL LIKE I
        SO GOOD I SMALL LIKE U

        SO GOOD TWIN NOT LIKE EVILTWIN
        SO GOOD TWIN KINDA LIKE ANTI EVILTWIN

    SO IM LIKE test_math MYSELF?
        BTW, testing +/-/*
        SO GOOD CHEEZBURGER ALONG WITH CHEEZBURGER KINDA LIKE TWINZ
        SO GOOD CHEEZBURGER TAKE AWAY CHEEZBURGER KINDA LIKE EASTERBUNNY
        SO GOOD FIV OF THOSE TWINZ KINDA LIKE ALLFINGERZ
        HE CAN HAS SIKS
        HE GETZ ANOTHR FOUR
        SO GOOD HE KINDA LIKE ALLFINGERZ
        HE THROWZ AWAY THR33
        SO GOOD HE KINDA LIKE SEVN
        HE COPIES HIMSELF BY TWO
        SO GOOD HE KINDA LIKE 14

        BTW, testing / and //
        SO GOOD ALLFINGERZ SMASHES INTO FIV KINDA LIKE TWICE
        GUMDROP CAN HAS ALLFINGERZ SMASHES INTO SEVN
        SO GOOD GUMDROP KINDA LIKE 0.14285714285714286E+01
        SO GOOD ALLFINGERZ SMASHES NICELY INTO SEVN KINDA LIKE 1
        HE CAN HAS ALLFINGERZ
        HE SMASHES HIMSELF INTO 3
        SO GOOD HE KINDA LIKE 3.3333333333333335
        HE SMASHES HIMSELF NICELY INTO 2
        SO GOOD HE KINDA LIKE 1.0

        BTW, testing **
        SO GOOD TWINZ BY GRAYSKULL POWER FIV KINDA LIKE 32
        HE CAN HAZ TWINS
        HE USES GRAYSKULL POWER FIV
        SO GOOD HE KINDA LIKE 32

        BTW, testing %
        SO GOOD ALLFINGERZ LEFTOVERS AFTER SMASHING INTO FOUR KINDA LIKE TWINS
        HE CAN HAZ SEVN
        HE IZ LEFTOVERS AFTER SMASHING HIMSELF INTO THR33
        SO GOOD HE KINDA LIKE CHEEZBURGER

        BTW, testing even/odd
        SO GOOD ATE EVEN
        SO GOOD NINE ODD

        BTW, testing @
        PONY CAN HAS SWIRLY_TESTER THING
        SO GOOD PONY OF THOSE SWIRLY THR33 KINDA LIKE ATE
        PONY COPIES ITSELF BY SWIRLY FOUR
        SO GOOD PONY KINDA LIKE ATE

        BTW, testing >> and <<
        SO GOOD FIV DANCE LEFT BY TWO KINDA LIKE TWO OF THOSE ALLFINGERZ
        SO GOOD SIKS DANCE RIGHT BY CHEEZBURGER KINDA LIKE THR33
        HE CAN HAZ TWO OF THOSE TWINS
        HE DANCES LEFT BY THR33
        SO GOOD HE KINDA LIKE 32
        HE DANCES RIGHT BY TWO
        SO GOOD HE KINDA LIKE ATE

        BTW, testing &, | and ^
        SO GOOD THR33 BOTH WITH FIV KINDA LIKE CHEEZBURGER
        SO GOOD CHEEZBURGER EITHER WITH TWINS KINDA LIKE THR33
        SO GOOD SIKS THARCANONLYBEONE WITH FOUR KINDA LIKE TWINS
        HE CAN HAS SIKS
        HE BECOMES BOTH WITH THR33
        SO GOOD HE KINDA LIKE TWO
        HE BECOMES EITHER WITH FOUR
        SO GOOD HE KINDA LIKE SIKS
        HE BECOMES THARCANONLYBEONE WITH TWINS
        SO GOOD HE KINDA LIKE FOUR

        BTW, testing ~
        SO GOOD OPPOSIT NINE KINDA LIKE TOEZ

        BTW, testing assorted other
        SHE CAN HAS EASTERBUNNY OR MABEE CHOKOLET
        SO GOOD SHE KINDA LIKE CHOKOLET
        SHE COPIES HERSELF BY FIV
        HE CAN HAS SHE KINDA LIKE ANTI 5
        SO GOOD HE KINDA LIKE YEAH
        DIALECTIC CAN HAS HE KINDA LIKE YEAH & HE KINDA NOT LIKE YEAH
        AINT GOOD DIALECTIC

    SO IM LIKE test_exceptions MYSELF?
        PLZ?
            CHEEZBURGER SMASHES INTO EASTERBUNNY
            HALP DONOTLIKE "divide by zero?" !
        O NOES BLACKHOLE?
            I IZ CUTE

        PLZ?
            HALP KeyError
        O NOES KeyError?
            I IZ CUTE
        O NOES?
            HALP DONOTLIKE !
        IZ KEWL?
            HALP DONOTLIKE !

    SO IM LIKE test_for_loop MYSELF?
        I CAN HAS INVISIBLE LIST
        GIMME EACH TOY IN THE NUMBRZ FIV OK?
            I ADDED TOY OK
        SO GOOD I KINDA LIKE SOME 0 AND 1 AND 2 AND 3 AND 4 !

        IT CAN HAS MEH
        GIMME EACH LETR IN THIS "HAI WERLD!"?
            I IZ CUTE
        ALL DONE?
            IT CAN HAS YEAH
        SO GOOD IT KINDA LIKE YEAH

    SO IM LIKE test_if MYSELF?
        IZ CHEEZBURGER KINDA LIKE 2?
            HALP DONOTLIKE 2 !
        OR IZ CHEEZBURGER KINDA LIKE 3?
            HALP DONOTLIKE 3 !
        I GIVE UP?
            I IZ CUTE

    SO IM LIKE test_invisible MYSELF?
        FRIEND CAN HAS INVISIBLE STRING ALONG WITH "ME"
        SO GOOD FRIEND KINDA LIKE "ME"
        FRIEND CAN HAS INVISIBLE LIST
        FRIEND ADDED INVISIBLE!
        FRIEND LOOK AT 0!      KINDA IS INVISIBLE

    SO IM LIKE test_loop MYSELF?
        I CAN HAS EASTERBUNNY
        U CAN HAS CHEEZBURGER
        WHILE I CUTE?
            I GETZ ANOTHR ONE
            IZ I EVEN?
                KTHX
            IZ I ATE MORE CHEEZBURGER THAN 100?
                KTHXBYE
            U GETZ ANOTHR I
        SO GOOD U KINDA LIKE 2501

    SO IM LIKE test_with MYSELF?
        WIF WIF_TESTER THING LIKE PET?
            SO GOOD PET KINDA LIKE "DOGGIE"

    SO IM LIKE test_lookup MYSELF?
        WEZ CAN HAS SOME
             BUCKET "name"? "Andrew" AND "country"? "Sweden" ! AND
             BUCKET "name"? "Guido" AND "country"? "US"! AND
             BUCKET "name"? "Johann" AND "country"? "Germany"!!

        SO GOOD WEZ LOOK AT 0! LOOK AT "name"! LOOK AT ANTI 1! KINDA LIKE "w"
        FIELDZ CAN HAS "name country" OWN split THING
        SO GOOD WEZ LOOK AT 2! LOOK AT FIELDZ LOOK AT 1!! KINDA LIKE "Germany"

    SO IM LIKE test_async MYSELF?

        SURKUL CAN HAZ ASYNKIO OWN get_event_loop THING
        RESULT CAN HAZ SURKUL OWN run_until_complete WIT DELAYED_TEST THING !
        SURKUL OWN close THING

        SO GOOD RESULT KINDA LIKE "HAPPINES"

IZ __name__ KINDA LIKE "__main__"?
        COMPLAIN "UNIT TESTS R LEET!"!
        VISIBLE 'READYSETGO!!!'!
        UNITTEST OWN main THING
